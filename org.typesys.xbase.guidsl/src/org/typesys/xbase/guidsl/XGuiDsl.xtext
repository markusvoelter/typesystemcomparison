grammar org.typesys.xbase.guidsl.XGuiDsl with org.eclipse.xtext.xbase.Xbase

generate xGuiDsl "http://www.typesys.org/xbase/guidsl/XGuiDsl"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/xtext/common/JavaVMTypes"

Model:
	elements+=AbstractDeclaration*;

AbstractDeclaration :
	Entity | Form
;
/*
 * Form and Widgets 
 */

Form:
	"form" name=ID "edits" entity=[Entity|QualifiedName] "{"	
		(widgets+=Widget)*
	"}"
;

Widget:
	TextWidget | CheckBoxWidget 
;

TextWidget:
	"text" "(" length=Number ")" "->" attr=[Attribute|QualifiedName] ("validate" validate=XExpression)? ";"
;

CheckBoxWidget:
	"checkbox" "->" attr=[Attribute|QualifiedName] ("validate" validate=XExpression)? ";"
;

/*
 * Entities and types, to be referred by forms
 */

Entity:
	"entity" name=ID ('extends' superType=JvmParameterizedTypeReference)? "{"
		(attributes+=Attribute)*
	"}"
;


Attribute:
	name=ID ( ((":" type=JvmParameterizedTypeReference)? "=" expr=XExpression) | 
               (":" type=JvmParameterizedTypeReference) )";"
;

JvmParameterizedTypeReference: 
	type=[JvmType|QualifiedName]
;

// TODO JvmParameterizedTypeReference: error if not overridden
// error(211): ../org.typesys.xbase.guidsl/src-gen/org/typesys/xbase/guidsl/parser/antlr/internal/InternalXGuiDsl.g:544:1: [fatal] rule ruleAttribute has non-LL(*) decision due to recursive rule invocations reachable from alts 1,2.  Resolve by left-factoring or using syntactic predicates or using backtrack=true option.

/*
 * In the Xbase variant, XExpression is used
 * for expressions
 */
// widgetcontent -> added to JvmModelInferrer
// lengthOf -> Xbase builtin String.length()