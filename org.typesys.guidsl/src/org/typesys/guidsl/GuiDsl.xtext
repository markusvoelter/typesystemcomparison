grammar org.typesys.guidsl.GuiDsl with org.eclipse.xtext.common.Terminals

generate guiDsl "http://www.typesys.org/guidsl/GuiDsl"
	
import "http://www.eclipse.org/emf/2002/Ecore" as ecore	

Model:
	(entities+=Entity | 
	 forms+=Form )*;

/*
 * Form and Widgets 
 */

Form:
	"form" name=ID "edits" entity=[Entity] "{"	
		(widgets+=Widget)*
	"}"
;

Widget:
	TextWidget | CheckBoxWidget 
;

TextWidget:
	"text" "(" length=DECIMAL_NUMBER ")" "->" attr=[Attribute] ("validate" validate=Expr)? ";"
;

CheckBoxWidget:
	"checkbox" "->" attr=[Attribute] ("validate" validate=Expr)? ";"
;

/*
 * Entities and types to be referred by forms
 */

Entity:
	"entity" name=ID "{"
		(attributes+=Attribute)*
	"}"
;

Attribute:
	SimpleAttribute | DerivedAttribute
;

DerivedAttribute:
	"/" name=ID "=" expr=Expr ";"
;

SimpleAttribute:
	name=ID ":" type=Type ";"
;

Type:
	PrimitiveType | EntityType
;

EntityType:
	ref=[Entity]
;


PrimitiveType:
	IntType | FloatType| BooleanType | StringType
;

FloatType:
	{FloatType} "float"
;

IntType:
	{IntType} "int"
;

BooleanType:
	{BooleanType} "bool"
;

StringType:
	{StringType} "string"
;

/*
 * Expressions, used in DSL form and widget
 */

Expr:
	ComparisonLevel;

ComparisonLevel returns Expression:
	AdditionLevel ((({Equals.left=current} "==") |
					({Unequals.left=current} "!=") |
					({Greater.left=current} ">") |
					({GreaterEquals.left=current} ">=") |
					({SmallerEquals.left=current} "<=") |
					({LogicalAnd.left=current} "&&") |
					({Implies.left=current} "=>") |
					({LogicalOr.left=current} "||") |
					({Smaller.left=current} "<"))
											right=AdditionLevel)*;
AdditionLevel returns Expression:
	MultiplicationLevel ((({Plus.left=current} "+") |
					     ({Minus.left=current} "-")) right=MultiplicationLevel)*;

MultiplicationLevel returns Expression:
	PrefixOpLevel ((({Multi.left=current} "*") |
					({Div.left=current} "/")) right=PrefixOpLevel)*;

PrefixOpLevel returns Expression:
	({NotExpression} "!" "(" expr=Expr ")") |
	Atomic;

Atomic returns Expression:
	({TrueExpr} "true") |
	({FalseExpr} "false") |
	({FieldContent} "widgetcontent") |
	({lenghtOf} "lengthOf" "(" expr=Expr ")" ) |
	({ParenExpr} "(" expr=Expr ")") |
	({NumberLiteral} value=DECIMAL_NUMBER) |
	({StringLiteral} value=STRING) |
	({AttributeRef} attr=[Attribute|ID]);

terminal DECIMAL_NUMBER returns ecore::EBigDecimal:
	("-")? ('0'..'9')* ('.' ('0'..'9')+)?;

terminal INT returns ecore::EInt:
	'this one has been deactivated';